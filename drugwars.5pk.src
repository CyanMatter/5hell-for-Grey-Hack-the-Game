// drug wars v 0.2 by Plu70
//
//
//

    // game time constants
    globals.TIMELIMIT = 31
    globals.GAMETIME  =  0
    globals.GOAL = "1337331"
    
    // class player
    PLAYER = {} 
    PLAYER.location=""
    PLAYER.inventory_size=100
    //PLAYER.inventory_contents=[]
    PLAYER.heat=0
    PLAYER.event_status=""
    
    //////////////// GRAPHICS /////////////////
    
    colorCyan = "<color=#00FFFF><b>"
    CT = "</b></color>"
    
    logo = ["D","R","U","G","W","A","R","S"]
    
    officerHardassSprite = "
                          _______________
                          \      __      /         __
                           \_____()_____/         /  )
                           '============`        /  /
                            #---\  /---#        /  /
                           (# @\| |/@  #)      /  /
                            \   (_)   /       /  /
                            |\ '---` /|      /  /
                    _______/ \\_____// \____/ o_|
                   /       \  /     \  /   / o_|
                  /|           o|        / o_| \
                 / |  _____     |       / /   \ \
                /  |  |===|    o|      / /\    \ \
               |   |   \@/      |     / /  \    \ \
               |   |___________o|__/----)   \    \/
               |   '              ||  --)    \     |
               |__________________||  --)     \    /
                   |           o|   ''''   |   \__/
                   |            |          |
                     ""Officer Hardass"" Img by Rosebud"
    
    
                                            
    deaSprite = "
       Art by Joan G. Stark
               _.---._
            .-' ((O)) '-.
             \ _.\_/._ /
              /..___..\
              ;-.___.-;
             (| e ) e |)     .;.
              \  /_   /      ||||
              _\__-__/_    (\|'-|
            /` / \V/ \ `\   \ )/
           /   \  Y  /   \  /=/
          /  |  \ | / {}  \/ /
         /  /|   `|'   |\   /
         \  \|    |.   | \_/
          \ /\    |.   |
           \_/\   |.   |
           /)_/   |    |
          // ',__.'.__,'
         //   |   |   |
        //    |   |   |
       (/     |   |   |
              |   |   |
              | _ | _ |
              |   |   |
              |   |   |
              |   |   |
        jgs   |___|___|
              /  J L  \
             (__/   \__)"
    
    
                    fbiSprite = "
                    FBI
                            _ _.-'`-._ _
                            ;.'________'.;
                _________n.[____________].n_________
                |''_''_''_''||==||==||==||''_''_''_'']
                |'''''''''''||..||..||..||'''''''''''|
                |LI LI LI LI||LI||LI||LI||LI LI LI LI|
                |.. .. .. ..||..||..||..||.. .. .. ..|
                |LI LI LI LI||LI||LI||LI||LI LI LI LI|
            ,,;;,;;;,;;;,;;;,;;;,;;;,;;;,;;,;;;,;;;,;;,,
            ;;jgs;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
                    "
    
    
    
                    elGuapoSprite = "
                    .------\ /------.
                    |       -       |
                    |               |
                    |               |
                    |               |
                 _______________________
                 ===========.===========
                   / ~~~~~     ~~~~~ \
                  /|     |     |\
                  W   ---  / \  ---   W
                  \.      |o o|      ./
                   |                 |
                   \    #########    /
                    \  ## ----- ##  /
                     \##         ##/
                      \_____v_____/"
    
    
            rabbitSprite = "
                     ____...                                  
              .-'--''''.__    `.                                
             |            `    |                                
    (         `._....------.._.:          
    )         .()''        ``().                                
    '          () .=='  `===  `-.         
    . )       (         g)                                
    )         )     /        J          
    (          |.   /      . (                                  
    $$         (.  (_'.   , )|`                                 
    ||         |\`-....--'/  ' \                                
    /||.         \\ | | | /  /   \.                              
    //||(\         \`-===-'  '     \o.                            
    .//7' |)         `. --   / (     OObaaaad888b.                 
    (<<. / |     .a888b`.__.'d\     OO888888888888a.               
    \  Y' |    .8888888aaaa88POOOOOO888888888888888.              
    \  \ |   .888888888888888888888888888888888888b              
    |   |  .d88888P88888888888888888888888b8888888.             
    b.--d .d88888P8888888888888888a:f888888|888888b             
    88888b 888888|8888888888888888888888888\8888888"
    
    
    
    downTownSprite = "
            __   __                     ___      _
           |  | |  |      /|           |   |   _/ \_
           |  | |  |  _  | |__         |   |_-/     \-_     _
         __|  | |  |_| | | |  |/\_     |   |  \     /  |___|
        |  |  | |  | | __| |  |   |_   |   |   |___|   |   |
        |  |  |^|  | ||  | |  |   | |__|   |   |   |   |   |
        |  |  |||  | ||  | |  |   | /\ |   |   |   |   |   |
        ~~~~~~~~~~~~~~~~~~~~~~~~~~~/  \~~~~~~~~~~~~~~~~~~~~~~~
       ~ ~~  ~ ~~ ~~~ ~ ~ ~~ ~~ ~~ \   \__   ~  ~  ~~~~ ~~~ ~~
     ~~ ~ ~ ~~~ ~~  ~~ ~~~~~~~~~~ ~ \   \o\  ~~ ~ ~~~~ ~ ~ ~~~
       ~ ~~~~~~~~ ~ ~ ~~ ~ ~ ~ ~ ~~~ \   \o\=   ~~ ~~  ~~ ~ ~~
    ~ ~ ~ ~~~~~~~ ~  ~~ ~~ ~ ~~ ~ ~ ~~ ~ ~ ~~ ~~~ ~ ~ ~ ~ ~~~~"
    
    
    villaSprite = "
                           X_x
                          / \\\
                          |n| |
                        )(|_|-'X
                       /  \\Y// \
                       |A | | |A|
                       |  | | |_|
                )(__X,,|__|MEB;;;-,)(,
               /  \\\;;;;;;;;;;;;/    \
               |A | |            | U  |
             )_|  | |____)-----( |    |
            ///|__|-'////       \|___)=(__X
           /////////////         \///   \/ \
           |           |  U    U |//     \u|
           |   )_,-,___|_)=(     | |  U  |_|_X
           |  ///   \\|//   \    | |  __ |/// \
         )_')(//     \Y/     >---)=( /  \|  | |-----------------..,
        //// ,\ u   u |   u /////   \|  ||__|A|----------------.., \,
       |  | .. |      |    ///// ,-, \__||--------------------.., \, \,
    ---'--'_::_|______'----| u | | | |-----------------------.., \, \, \,
                           |___|_|_|_|----------------------.., \, \, \, \,
    -------------------------------------------------------.., \, \, \, \, \
                                                                      \Valkyrie"
    
    
    gcccSprite = "
                                                              |>>>
                       _                      _                |
        ____________ .' '.    _____/----/-\ .' './========\   / \
       //// ////// /V_.-._\  |.-.-.|===| _ |-----| u    u |  /___\
      // /// // ///==\ u |.  || | ||===||||| |T| |   ||   | .| u |_ _ _ _ _ _
     ///////-\////====\==|:::::::::::::::::::::::::::::::::::|u u| U U U U U
     |----/\u |--|++++|..|'''''''''''::::::::::::::''''''''''|+++|+-+-+-+-+-+
     |u u|u | |u ||||||..|              '::::::::'           |===|>=== _ _ ==
     |===|  |u|==|++++|==|              .::::::::.           | T |....| V |..
     |u u|u | |u ||HH||         \|/    .::::::::::.
     |===|_.|u|_.|+HH+|_              .::::::::::::.              _
                    __(_)___         .::::::::::::::.         ___(_)__
    ---------------/  / \  /|       .:::::;;;:::;;:::.       |\  / \  \-------
    ______________/_______/ |      .::::::;;:::::;;:::.      | \_______\________
    |       |     [===  =] /|     .:::::;;;::::::;;;:::.     |\ [==  = ]   |
    |_______|_____[ = == ]/ |    .:::::;;;:::::::;;;::::.    | \[ ===  ]___|____
         |       |[  === ] /|   .:::::;;;::::::::;;;:::::.   |\ [=  ===] |
    _____|_______|[== = =]/ |  .:::::;;;::::::::::;;;:::::.  | \[ ==  =]_|______
     |       |    [ == = ] /| .::::::;;:::::::::::;;;::::::. |\ [== == ]      |
    _|_______|____[=  == ]/ |.::::::;;:::::::::::::;;;::::::.| \[  === ]______|_
       |       |  [ === =] /.::::::;;::::::::::::::;;;:::::::.\ [===  =]   |
    ___|_______|__[ == ==]/.::::::;;;:::::::::::::::;;;:::::::.\[=  == ]___|_____"
    
    shabbyTownSprite = "
                  )
                 (      _[]_         (
         __[]___[]___[]/____\_[]_    )
        /______________|[][]|____\  (
        |[][]|[][]|[][]|[][]|[][]|__[]_
        |  /\|/\  |  /\|  /\|/\  |_____\
        |[]|||||[]|[]|||[]|||||[]|[_]|||
      ===================================
      -  -  -  -  -  -  -  -  -  -  -  -  -
    =========================================   Tom Harvey"
    
    districtSprite = "
                      \  |  /         ___________
       ____________  \ \_# /         |  ___      |       _________
      |            |  \  #/          | |   |     |      | = = = = |
      | |   |   |  |   \\#           | |`v'|     |      |         |
      |            |    \#  //       |  --- ___  |      | |  || | |
      | |   |   |  |     #_//        |     |   | |      |         |
      |            |  \\ #_/_______  |     |   | |      | |  || | |
      | |   |   |  |   \\# /_____/ \ |      ---  |      |         |
      |            |    \# |+ ++|  | |  |~~~~~~| |      | |  || | |
      |            |    \# |+ ++|  | |  |~~~~~~| |      | |  || | |
    ~~|    (~~~~~) |~~~~~#~| H  |_ |~|  | |||| | |~~~~~~|         |
      |    ( ||| ) |     # ~~~~~~    |  | |||| | |      | ||||||| |
      ~~~~~~~~~~~~~________/  /_____ |  | |||| | |      | ||||||| |
                                     ~~~~~~~~~~~~~      | ||||||| |"
    
    
    shadyAcresSprite = "
      ~         ~~          __
           _T      .,,.    ~--~ ^^
     ^^   // \                    ~
          ][O]    ^^      ,-~ ~
       /''-I_I         _II____
    __/_  /   \ ______/ ''   /'\_,__
      | II--'''' \,--:--..,_/,.-{ },
    ; '/__\,.--';|   |[] .-.| O{ _ }
    :' |  | []  -|   ''--:.;[,.'\,/
    '  |[]|,.--'' '',   ''-,.    |
      ..    ..-''    ;       ''. '  Steven Maddison"
    
    lasGreygasSprite = "
                                     .''.
           .''.             *''*    :_\/_:     . 
          :_\/_:   .    .:.*_\/_*   : /\ :  .'.:.'.
      .''.: /\ : _\(/_  ':'* /\ *  : '..'.  -=:o:=-
     :_\/_:'.:::. /)\*''*  .|.* '.\'/.'_\(/_'.':'.'
     : /\ : :::::  '*_\/_* | |  -= o =- /)\    '  *
      '..'  ':::'   * /\ * |'|  .'/.\'.  '._____
          *        __*..* |  |     :      |.   |' .---'|
           _*   .-'   '-. |  |     .--'|  ||   | _|    |
        .-'|  _.|  |    ||   '-__  |   |  |    ||      |
        |' | |.    |    ||       | |   |  |    ||      |
     ___|  '-'     '    ''      '-'   '-.'    '`      |____
    jgs~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    "
    capitolSprite = "
                                 ^
                    _______     ^^^
                   |xxxxxxx|  _^^^^^_
                   |xxxxxxx| | [][]  |
                ______xxxxx| |[][][] |
               |++++++|xxxx| | [][][]|      
               |++++++|xxxx| |[][][] |
               |++++++|_________ [][]|
               |++++++|=|=|=|=|=| [] |
               |++++++|=|=|=|=|=|[][]|
    ___________|++HH++|  _HHHH__|   _________   _________  _________
             _______________   ______________      ______________
    __________________  ___________    __________________    ____________"
    
    
    
    
    /////////////////////////////////////////////
    
    ////// DRUGS /////////
    drug = {}
    drug.price=0
    drug.size=0
    drug.event_status=""
    drug.availability=1
    drug.held=0
    drug.buy = function(amount)  // handles moving inventory. does not handle payment. takes player object, integer
        if typeof(amount) != "number" or amount < 1 or amount > self.availability then return 0
        if player.cash < self.cost * amount then return 0
        if self.size * amount <= player.inventory_size then self.held = self.held + amount else return 0
        player.debit( (self.cost * amount) )
        player.update_inventory( "in", (self.size * amount) )
        self.availability = self.availability - amount
        return 1
    end function 
    drug.sell = function(amount) // handles moving inventroy. does not handle payment. takes player object, integer
        if typeof(amount) != "number" or amount < 1 then return 0
        if self.held >= amount then self.held = self.held - amount else return 0
        player.credit( (self.cost * amount) )
        player.update_inventory("out", self.size * amount)
        self.availability = self.availability + amount
        return 1
    end function
    
    //
    
    cannabis = new drug
    cannabis.name = "cannabis"
    cannabis.price = 100
    cannabis.size = 4
    cannabis.cost = 100
    cannabis.icon = "<color=green><b>_\||/_</b></color>"
    
    mushrooms = new drug
    mushrooms.name = "mushrooms"
    mushrooms.price = 50
    mushrooms.size = 2
    mushrooms.cost = 50
    mushrooms.icon = "<color=white><b>.<color=red>`<color=white>||</color>`</color>.</b></color>"
    
    molly = new drug
    molly.name = "molly"
    molly.price = 20
    molly.size = 1
    molly.cost = 20
    molly.icon = "<color=orange><b>..(o).</b></color>"
    
    cocaine = new drug
    cocaine.name = "cocaine"
    cocaine.price = 500
    cocaine.size = 10
    cocaine.cost = 500
    cocaine.icon = "<color=white><b>.[==].</b></color>"
    
    heroin = new drug
    heroin.name = "heroin"
    heroin.price = 1000
    heroin.size = 20
    heroin.cost = 1000
    heroin.icon = "<color=#272626ff><b>.====.</b></color>"
    
    lean = new drug
    lean.name = "lean"
    lean.price = 300
    lean.size = 4
    lean.cost = 300
    lean.icon = "<color=purple><b>.<u>|~~|</u>.</b></color>"
    
    crack = new drug
    crack.name = "crack"
    crack.price = 200
    crack.size = 5
    crack.cost = 200
    crack.icon = "<b><color=white>.<*<*></b></color>"
    
    acid = new drug
    acid.name = "acid"
    acid.price = 400
    acid.size = 3
    acid.cost = 400
    acid.icon = "<b><color=blue>.{#}..</b></color>"
    _drogas = [cannabis,mushrooms,molly,cocaine,heroin,lean,crack,acid]
    
    //////// END DRUGS /////////
    
    //////// NEIGHBORHOODS /////////
    
    neighborhood = {}
    neighborhood.name=""
    neighborhood.wealth=""
    neighborhood.population=""
    neighborhood.drug_modifiers=""
    neighborhood.event_status=""
    
    downtown = new neighborhood
    downtown.name = "Downtown"
    downtown.wealth = 1000
    downtown.population = 100
    downtown.drug_modifiers =    {"cannabis":"medium","mushrooms":"medium","molly":"medium","cocaine":"high","heroin":"high","lean":"medium","crack":"medium","acid":"medium"}
    downtown.sprite = downTownSprite
    
    villa = new neighborhood
    villa.name = "Greyton's Villa"
    villa.wealth = 5000
    villa.population = 10
    villa.drug_modifiers =      {"cannabis":"low","mushrooms":"low","molly":"low","cocaine":"high","heroin":"high","lean":"medium","crack":"low","acid":"medium"}
    villa.sprite = villaSprite
    
    gccc = new neighborhood
    gccc.name = "Grey County Community College"
    gccc.wealth = 500
    gccc.population = 300
    gccc.drug_modifiers =       {"cannabis":"high","mushrooms":"high","molly":"medium","cocaine":"medium","heroin":"medium","lean":"medium","crack":"medium","acid":"low"}
    gccc.sprite = gcccSprite
    
    shabbytown = new neighborhood
    shabbytown.name = "Shabbytown"
    shabbytown.wealth = 200
    shabbytown.population = 100
    shabbytown.drug_modifiers = {"cannabis":"medium","mushrooms":"medium","molly":"medium","cocaine":"low","heroin":"medium","lean":"high","crack":"high","acid":"medium"}
    shabbytown.sprite = shabbyTownSprite
    
    district = new neighborhood
    district.name = "District 108"
    district.wealth = 350
    district.population = 50
    district.drug_modifiers =  {"cannabis":"low","mushrooms":"medium","molly":"low","cocaine":"medium","heroin":"high","lean":"high","crack":"high","acid":"medium"}
    district.sprite = districtSprite
    
    shady = new neighborhood
    shady.name = "Shady Acres"
    shady.wealth = 800
    shady.population = 75
    shady.drug_modifiers =    {"cannabis":"high","mushrooms":"low","molly":"high","cocaine":"medium","heroin":"low","lean":"medium","crack":"medium","acid":"medium"}
    shady.sprite = shadyAcresSprite
    
    greygas = new neighborhood
    greygas.name = "Las Greygas"
    greygas.wealth = 1000
    greygas.population = 300
    greygas.drug_modifiers =  {"cannabis":"low","mushrooms":"high","molly":"high","cocaine":"high","heroin":"low","lean":"low","crack":"medium","acid":"high"}
    greygas.sprite = lasGreygasSprite
    
    capitol = new neighborhood
    capitol.name = "Capitol Heights"
    capitol.wealth = 1000
    capitol.population = 150
    capitol.drug_modifiers =  {"cannabis":"medium","mushrooms":"medium","molly":"medium","cocaine":"high","heroin":"medium","lean":"low","crack":"medium","acid":"low"}
    capitol.sprite = capitolSprite
    
    _hoods = [ downtown, villa, gccc, shabbytown, district, shady, greygas, capitol ]
    if DEBUG then
        for h in _hoods
            print h.name // debug
        end for
    end if
    /////// END NEIGHBORHOODS ///////
    
    /////// EVENTS ///////////////
    event = {}
    event.names=["Officer_Hardass","The_DEA","The_FBI","El_Guapo","Rabbit"]
    //event.affects = ["player", "drug", "neighborhood"]
    //event.effect = ["price", "availability", "heat","shop"]
    event.Rabbit = function()
        print
        print rabbitSprite
        print
        print "A shadow fellow pulls you into an alley and offers you a deal."
        print "<color=yellow>Wanna buy a trenchcoat for an extra 20 inventory? $1000 cash, bub."
        print
        choice = user_input("[1] or Yes (anything else for no):> ")
        if choice == "1" or choice.lower == "yes" then 
            if player.cash >= 1000 then 
                print
                print "<color=yellow>Here you go. Hmm, looks good on you."
                player.cash = player.cash - 1000
                player.inventory_size = player.inventory_size + 20
                player.event_status = "none"
            else 
                print
                print "<color=yellow>You don't have the cash, bub!"
            end if
        else 
            print
            print "<color=yellow>A'ight, cool. I know someone else that wants it."
        end if
        return null
    end function 
    event.El_Guapo = function()
        print 
        print elGuapoSprite
        print
        print "El Guapo's narco sub has made berth. "
        print "The market has been flooded with cheap drugs!"
        print "Deals like these won't last long!"
        player.event_status = "none"
        return user_input("press <<b>enter</b>> to continue"+char(10))
    end function 
    event.The_FBI = function()
        print 
        print fbiSprite
        print
        print "The FBI opened an investigation into you! "
        print "They are demanding $"+ceil(player.cash / 10)+" in bribes to avoid jail time!"
        print "You fill a duffel bag with the money and head to the FBI field office."
        print "When you arrive you dump the cash on their doorstep."
        print "You decide to keep the duffelbag. "
        print "Your inventory space has increased by 10!"
        player.cash = ceil(player.cash / 10)
        player.inventory_size = player.inventory_size + 10
        player.event_status = "none"
        return user_input("press <<b>enter</b>> to continue"+char(10))
    end function 
    event.The_DEA = function()
        print 
        print deaSprite
        print
        print "The DEA made a major bust! \nDrug availability has temporarily gone down! \nPrices have permanently gone up!"
        player.event_status = "none"
    
        return user_input("press <<b>enter</b>> to continue"+char(10))
    end function 
    event.Officer_Hardass = function()
    
        print 
        print officerHardassSprite
        print
        print "Officer Hardass caught you slinging dope and beat your ass! "+char(10)+"He took all your drugs and you had to bribe him with $"+ceil(player.cash / 10)+" to avoid jail time."
        print "Barely able to stand, you wonder if you will survive another day."
        player.cash = player.cash - ceil(player.cash / 10)
        for d in _drogas 
            a = d.held
            d.sell(a)
            player.debit(d.cost * a)
        end for
        player.event_status = "none"
        return user_input("press <<b>enter</b>> to continue"+char(10))
    end function 
    ////// END EVENTS //////////
    
    // win condition
    _score = function()
        if player.cash >= GOAL.to_int then return("You win! :]") else return("You lose. :[")
        return null
    end function
    
    // each time the player moves we tick the clock up one.
    _game_time_update = function()
        globals.GAMETIME = GAMETIME + 1
        status = null
        if GAMETIME >= TIMELIMIT then status = (_score)
        if DEBUG then print "debug: tick"
        if DEBUG then print "debug: gametime: "+GAMETIME
        player.event_status = "none"
        if event.names.len > 0 then
            ev = range(0,9)
            ev.shuffle 
            e = ev.pop 
            event.names.shuffle   
            if DEBUG then 
                player.event_status = event.names.pop 
            else 
                if e == 9 then player.event_status = event.names.pop
            end if
        end if 
        return status
    end function 
    
    // drug price and availability based on popularity and population
    _update_drug_prices = function(locale)
        price = {}
        price.low = [1,1.5,2,2.5,3]
        price.medium = [3.5,4,5,6,4]
        price.high = [6.5,7,8,9,2]
    
        for d in _drogas 
            // modify prices
            r = [0,1,2,3,4]
            r.shuffle 
            if DEBUG then print "debug: "+d.name+char(10)+"debug: "+locale.name
            mod = price[locale.drug_modifiers[d.name]][r[0]]
            d.cost = d.price * mod
            d.availability = ceil(locale.population / mod)
            if player.event_status == "El_Guapo" then 
                d.availability = d.availability * 2
                d.cost = ceil(d.cost / 1.5)
            end if
            if player.event_status == "The_DEA" then 
                d.price = d.price + (d.price / 10)
                d.availability = ceil(d.availability / 2)
                d.cost = ceil(d.cost * 1.5)
            end if 
            if DEBUG then print "debug: cost: "+ d.cost
            if DEBUG then print "debug: avail: "+ d.availability
        end for 
    end function
    
    ///////////////////////////////////////
    //////// PLAYER ONE /////////
    
    player = new PLAYER 
    player.location = downtown // player starts here
    player.inventory_size=100
    //player.inventory_contents=[]
    player.heat=0
    player.event_status="none"
    player.cash = 100
    //
    player.update_location = function(moveto) // expects neighborhood object
        if DEBUG then print "debug: moveto: "+moveto.name // debug
        if self.location != moveto then self.location = moveto else return 0
        stat = _game_time_update
        _update_drug_prices(self.location)
        if player.event_status != "none" then event[player.event_status]
        return 1
    end function
    //
    player.debit = function( amount )
        if typeof(amount) != "number" or amount > player.cash or amount < 1 then return 0
        self.cash = self.cash - amount 
        return 1
    end function 
    player.credit = function( amount )
        if typeof(amount) != "number" or amount < 1 then return 0
        self.cash = self.cash + amount 
        return 1
    end function
    //
    player.update_inventory = function( direction, amount )
        if direction == "in" then self.inventory_size = self.inventory_size - amount
        if direction == "out" then self.inventory_size = self.inventory_size + amount 
        return null
    end function
    //player.property = ["none"]
    //player.net worth = 100
    //////////////////////////////////////
    
    move_menu = function(pr)
        print "current location: "+pr.location.name
        for h in _hoods
            //print h
            print "[<b>"+_hoods.indexOf(h)+"</b>] - "+h.name
        end for 
        choice = user_input("Move to :> ").to_int// int
        if choice == "" then return pr.location
        if DEBUG then print "debug: choice: "+choice
        if typeof(choice) == "number" and choice >= 0 and choice < _hoods.len then return _hoods[choice] else return pr.location // if we didn't move, return current location
    end function 
    
    drug_menu = function()
        print // something
        buf = []
        for d in _drogas
            buf.push("[<b>"+_drogas.indexOf(d)+"</b>] - "+d.name+" $"+d.cost+" "+d.availability+" units available, "+d.held+"/"+ ( floor( player.inventory_size / d.size ) + d.held ) + " units held "+d.icon )
        end for 
        print format_columns(buf.join(char(10)))
        choice = user_input(":> ")
        if choice == "" then return 
        if typeof(choice.to_int) == "number" and choice.to_int >= 0 and choice.to_int < _drogas.len and _drogas.hasIndex(choice.to_int) then return _drogas[choice.to_int] else print "Invalid selection."
        return 0
    end function 
    
    
    playing = true
    
    // game loop
    while playing == true
        if not DEBUG then clear_screen
        print    
        print player.location.sprite
        print
        // show location
        print "<u><color=#40865bff>Location: <b>"+player.location.name+"</b></u> <u>Days Left: <b>"+ (31 - GAMETIME)
        // show inventory
        print "<u><color=#40865bff>Inventory: <b>"+player.inventory_size+"</b> pocket space available."
        d_b = []
        print "- - - - - - - - - - - - - - - - - - - -"
        for d in _drogas
            d_b.push(  "<u><color=#40865bff>"+d.name.upper+" |"+colorCyan+d.held+CT+"|</u> <color=red>"+logo.pull+"</color> ["+d.icon+"]" )
        end for
        print format_columns(d_b.join(char(10)))
    
        // show cash
        print 
        print "<color=#40865bff>Cash: <color=yellow><b>$"+player.cash+"</color> Goal: >= <color=yellow>"+GOAL
        print
    
        // show prompt
        if player.event_status != "none" then print "[<color=yellow>E</color>] - "+player.event_status
        print "[<color=white>1</color>] - Buy [<color=white>2</color>] - Sell [<color=white>3</color>] - Move [<color=white>q</color>] - quit"
    
        // handle choices
        choice = user_input(":> ")
    
        // quit
        if choice == "q" then return "goodbye"
    
        // handle [rabbit] event
        if choice.lower == "e" and player.event_status != "none" then 
            event[player.event_status]
            user_input("press <<b>enter</b>> to continue")
            continue
        end if
    
        // -- buy drugs
        if choice == "1" then 
            drug_to_buy = drug_menu
            if drug_to_buy then print "Buying "+drug_to_buy.name else continue
            if drug_to_buy.buy(user_input("Amount :> ").to_int) then print "Success." else print "Failed."
            user_input("press <<b>enter</b>> to continue")
            continue
        end if
    
        // -- sell drugs
        if choice == "2" then 
            drug_to_sell = drug_menu
            if drug_to_sell then print "Selling "+drug_to_sell.name else continue
            if drug_to_sell.sell(user_input("Amount :> ").to_int) then print "Success." else print "Failed."
            user_input("press <<b>enter</b>> to continue")
            continue
        end if
    
        // -- move location // update prices, availability and location
        if choice == "3" then
            move = player.update_location(move_menu(player))
            if move == 1 then print "Travelling..." 
            if move == 0 then print "Staying put."
            if typeof(move) == "string" then return move // game over win/lose
            user_input("press <<b>enter</b>> to continue")
            continue
        end if
    
        // repeat until timelimit
        // exit with score
    end while
    
    
    
